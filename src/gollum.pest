// Low-level helpers
whitespace = _{ " " | "\t" | "\n" | "\r" }
comment = _{ "#" ~ (!("\r" | "\n") ~ any)* ~ "\n"* }
word = _{ 'a'..'z' | 'A'..'Z' | "_" }
digit = _{ '0'..'9' }
ident = _{ word ~ (word | digit)* }

// Atoms
variable = @{ ident }
boolean = @{ "true" | "false" }
int = @{ "-"? ~ digit+ }

// Top-level term
program = _ { soi ~ expression ~ eoi }

// Expressions
expression = { expression_paren ~ func_args* ~ ascription? }
expression_paren = _{ ( "(" ~ expression ~ ")" ) | expression_body }
expression_body = _{ condition | abstraction | boolean | variable | int }

bracketed = _{ "{"~ expression ~ "}" }

condition = {
  "if" ~ expression ~ bracketed ~ "else" ~ bracketed
}

expressions = { ( expression ~ ( "," ~ expression )* ~ ","? )? }
variables = { ( variable ~ ( "," ~ variable )* ~ ","? )? }

abstraction = { "fn" ~ "(" ~ variables ~ ")" ~ bracketed }

func_args = { "(" ~ expressions ~ ")" }

ascription = { ":" ~ typ }

// Types
typ = { variable ~ ( "->" ~ typ )* }
